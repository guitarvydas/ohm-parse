Ohm {
Grammar = GrammarName Inheritance? "{" Rule+ "}"
Inheritance = inheritanceOp GrammarName
Rule = RuleDefName FormalParameterList? Description? defineOp RuleBody+
Comment = CommentToEOL | MultiLineComment
defineOp = "=" | "+=" | ":="
RuleBody = Expr Tag?
SingleLineComment = "//" CommentToEOL
MultiLineComment = "/*" CommentToEOL+ "*/"
CommentToEOL = #(commentText eol)

FormalParameterList = "<" RefName CommaName ">"
Description = "(" descriptionText ")"
CommaName = ("," RefName)+

Expr = TaggedItemSequence Alternate*
Alternate = "|" TaggedItemSequence
Tag  = "--" TagName

TaggedItemSequence = ItemSequence Tag?
ItemSequence = Item+
Item = Lexification
Lexification = "#"? Prefixed
Prefixed
  = "~" Suffixed -- not
  | "&" Suffixed -- lookahead
  |     Suffixed -- noprefix


Suffix = "*" | "+" | "?"

Suffixed
  = Primary Suffix -- withSuffix
  | Primary        -- noSuffix

Primary = TerminalRange | Terminal | NestedExpr | RuleApplication
Terminal = string
TerminalRange = Terminal ".." Terminal
RuleApplication
  = RefName ActualParameterList -- parameterized
  | RefName                     -- unparameterized
NestedExpr = "(" Expr ")"

ActualParameterList = "<" Expr CommaExpr? ">"
CommaExpr = ("," Expr)+

TagName = name ~defineOp
RefName = name ~defineOp
RuleDefName = name &DefineFollow
GrammarName = name &GrammarNameFollow

DefineFollow = defineOp | Description | FormalParameterList
GrammarNameFollow = inheritanceOp | "{"

inheritanceOp = "<:"

string = dq stringChar* dq
dq = "\""
stringChar
  = "\\" "n" -- newline
  | "\\" "t" -- tab
  | "\\" "\"" -- dquote
  | "\\" "\\" -- backslash
  | "{" -- lbrace
  | "}" -- rbrace
  | "(" -- lpar
  | ")" -- rpar
  | "[" -- lbracket
  | "]" -- rbracket
  | "<" -- lt
  | ">" -- gt
  | ":" -- colon
  | notDQ    -- other
notDQ = ~dq any

name = nameFirst nameRest?
nameFirst = letter | "_"
nameRest = (digit | nameFirst)+

commentText = (~nl any)+ &eol
descriptionText = (~")" any)+

eol = (nl | end)
nl = "\n"


}

